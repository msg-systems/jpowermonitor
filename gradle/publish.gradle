publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId = project.group
            artifactId = project.name
            version = project.version
            from components.java

            pom {
                name = "${rootProject.name}"
                description = project.description
                url = 'https://github.com/msg-systems/jpowermonitor'

                scm {
                    connection = 'scm:git:https://github.com/msg-systems/jpowermonitor.git'
                    developerConnection = 'scm:git:git@github.com/msg-systems/jpowermonitor.git'
                    url = 'https://github.com/msg-systems/jpowermonitor.git'
                }

                licenses {
                    license {
                        name = 'Apache License, Version 2.0 (Apache-2.0)'
                        url = 'https://opensource.org/license/apache-2-0/'
                        distribution = 'repo'
                    }
                }
                developers {
                    developer {
                        id = 'dev1'
                        name = 'Jakob Deiner'
                        email = 'jakob.deiner@msg.group'
                    }
                    developer {
                        id = 'dev2'
                        name = 'Hans-Peter Keilhofer'
                        email = 'hans-peter.keilhofer@msg.group'
                    }
                }
            }
        }
    }
    repositories {
        maven {
            credentials {
                username = project.hasProperty("ossrhToken") ? project.property('ossrhToken') : null
                password = project.hasProperty("ossrhTokenPassword") ? project.property('ossrhTokenPassword') : null
            }
            url = 'https://ossrh-staging-api.central.sonatype.com/service/local/staging/deploy/maven2/'
        }
    }
}
// Signature of artifacts
if (project.hasProperty('signing.gnupg.keyName')) {
    signing {
        useGpgCmd()
        required = !rootProject.version.endsWith('-SNAPSHOT')
        sign publishing.publications.mavenJava
    }
} else {
    logger.info("Disable signing of artifacts as no key is configured")
}
