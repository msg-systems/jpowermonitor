buildscript {
    repositories {
        maven {
            url = "https://plugins.gradle.org/" // fix missing jcenter dependencies for e.g. cyclonedx plugin
        }
    }
}
plugins {
    id 'java-library'
    id 'maven-publish'
    id 'jacoco'
    id 'com.adarshr.test-logger' version "4.0.0"
    id 'com.github.ben-manes.versions' version "0.53.0"
    id "io.freefair.lombok" version "9.0.0"
    id "com.gradleup.shadow" version "9.2.2"
    id 'com.github.jk1.dependency-license-report' version "2.9"
    id 'org.cyclonedx.bom' version "3.0.1"
    id "org.jreleaser" version '1.21.0'
}

group = 'io.github.msg-systems'
version = rootProject.file('version.txt').text.trim()
description = 'JUnit Extension and Java Agent for energy consumption measurement. See also https://github.com/msg-systems/jpowermonitor'
java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}
tasks.withType(JavaCompile).configureEach {
    options.encoding = "UTF-8"
    // options.compilerArgs << '-Xlint:deprecation'
    options.compilerArgs << '-parameters'
}
repositories {
    mavenCentral()
}

configurations {
    demo
}

dependencies {
    implementation(
        'org.apache.httpcomponents.client5:httpclient5:5.5.1',
        'com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.20.0',
        'org.yaml:snakeyaml:2.5',
        'io.prometheus:simpleclient:0.16.0',
        'io.prometheus:simpleclient_common:0.16.0',
        'io.prometheus:simpleclient_hotspot:0.16.0',
        'io.prometheus:simpleclient_httpserver:0.16.0',
        'org.slf4j:slf4j-api:2.0.17'
    )
    demo(
        'org.slf4j:slf4j-api:2.0.17',
        'org.slf4j:slf4j-simple:2.0.17'
    )
    compileOnly(
        'org.jetbrains:annotations:26.0.2-1',
        'org.junit.jupiter:junit-jupiter:6.0.0'
    )
    testRuntimeOnly("org.junit.platform:junit-platform-launcher")
    testImplementation(
        // use logger only in test implementation in order to have a minimal set of dependencies in main source
        'org.slf4j:slf4j-simple:2.0.17',
        'org.junit.jupiter:junit-jupiter:6.0.0',
        'org.assertj:assertj-core:3.27.6'
    )
}

apply from: "gradle/test-config.gradle"
apply from: "gradle/jar.gradle"
apply from: "gradle/dependencyUpdates.gradle"
apply from: "gradle/publish.gradle"
apply from: "gradle/signing.gradle"
apply from: "gradle/dist.gradle"
import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar
import com.github.jk1.license.filter.LicenseBundleNormalizer
import com.github.jk1.license.render.InventoryHtmlReportRenderer

// --------------------- shadowJar
shadowJar {
    exclude("*.yaml") // exclude template.yaml
    exclude("*.json") // exclude grafana dashboards
    exclude("simplelogger.properties")
    exclude("group/msg/jpowermonitor/demo")
    exclude("group/msg/jpowermonitor/demo/*")
    dependencies {
        exclude(dependency('org.slf4j:slf4j-simple'))
    }
}

tasks.register('shadowJarDemo', ShadowJar) {
    mergeServiceFiles()
    group = "shadow"
    archiveBaseName = 'jpowermonitor-demo'
    configurations = [project.configurations.demo]
    manifest {
        attributes('Main-Class': 'group.msg.jpowermonitor.demo.StressCpuExample')
    }
    from(sourceSets.main.output) {
        include 'group/msg/jpowermonitor/demo/StressCpuExample.class' // Pfad zur gewÃ¼nschten Klasse
        include 'simplelogger.properties'
    }
}

shadowJar.dependsOn(shadowJarDemo)

// --------------------- license-report:

licenseReport {
    renderers = [new InventoryHtmlReportRenderer()]
    filters = [new LicenseBundleNormalizer(bundlePath: "$projectDir/gradle/license-normalizer-bundle.json")]
}

// --------------------- SBOM:
tasks.cyclonedxDirectBom {
    // includeConfigs is the list of configuration names to include when generating the BOM
    // (leave empty to include every configuration)
    includeConfigs = []
    // skipConfigs is a list of configuration names to exclude when generating the BOM
    skipConfigs = ["testCompileClasspath", "testRuntimeClasspath"]
}
