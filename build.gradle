buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/" // fix missing jcenter dependencies for e.g. cyclonedx plugin
        }
    }
}
plugins {
    id 'java-library'
    id 'maven-publish'
    id 'jacoco'
    id 'com.adarshr.test-logger' version "4.0.0"
    id 'com.github.ben-manes.versions' version "0.51.0"
    id "io.freefair.lombok" version "8.6"
    id "com.github.johnrengelman.shadow" version "8.1.1"
    id 'com.github.jk1.dependency-license-report' version "2.5"
    id 'org.cyclonedx.bom' version "1.8.2"
}

group = 'io.github.msg-systems'
version = rootProject.file('version.txt').text.trim()
description='JUnit Extension and Java Agent for energy consumption measurement. See also https://github.com/msg-systems/jpowermonitor'

sourceCompatibility = JavaVersion.VERSION_11
targetCompatibility = JavaVersion.VERSION_11

repositories {
    mavenCentral()
}
dependencies {
    implementation(
        [group: 'org.apache.httpcomponents.client5', name: 'httpclient5', version: '5.3'],
        [group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-jsr310', version: "2.16.1"],
        [group: 'org.yaml', name: 'snakeyaml', version: "2.2"],
    )
    compileOnly(
        [group: 'org.jetbrains', name: 'annotations', version: "24.1.0"],
        [group: 'org.junit.jupiter', name: 'junit-jupiter', version: "5.10.1"],
    )
    testImplementation(
        // use logger only in test implementation in order to have a minimal set of dependencies in main source
        [group: 'org.slf4j', name: 'slf4j-api', version: "2.0.11"],
        [group: 'org.junit.jupiter', name: 'junit-jupiter', version: "5.10.1"],
        [group: 'ch.qos.logback', name: 'logback-classic', version: '1.4.14'],
        [group: 'org.assertj', name: 'assertj-core', version: "3.25.1"]
    )
}

apply from: "gradle/test-config.gradle"
apply from: "gradle/jar.gradle"
apply from: "gradle/dependencyUpdates.gradle"
apply from: "gradle/publish.gradle"
apply from: "gradle/signing.gradle"
apply from: "gradle/dist.gradle"

// --------------------- shadowJar
shadowJar {
    exclude("*.yaml")
    exclude("logback.xml")
}

// --------------------- license-report:
import com.github.jk1.license.filter.LicenseBundleNormalizer
import com.github.jk1.license.render.InventoryHtmlReportRenderer

licenseReport {
    renderers = [new InventoryHtmlReportRenderer()]
    filters = [new LicenseBundleNormalizer(bundlePath: "$projectDir/gradle/license-normalizer-bundle.json")]
}

// --------------------- SBOM:
cyclonedxBom {
    // includeConfigs is the list of configuration names to include when generating the BOM
    // (leave empty to include every configuration)
    includeConfigs = []
    // skipConfigs is a list of configuration names to exclude when generating the BOM
    skipConfigs = ["testCompileClasspath", "testRuntimeClasspath"]
}
